#!/usr/bin/expect -f

set retval 0
set timeout -1
set loginUser "pi"
set loginPassword "raspberry"

spawn qemu-system-aarch64 -machine "raspi3b" -cpu arm1176 -m 1G -drive "format=raw,file=raspios.img" -dtb "./custom.dtb" -kernel "./tests/kernel/kernel8.img" -append "loglevel=0 quiet console=ttyAMA0,115200 root=/dev/mmcblk0p2 rootfstype=ext4 fsck.repair=yes rootwait init=/bin/bash" -nographic -serial mon:stdio

# Setup user
expect "root@(none):/# "
send "mount -o remount,rw /dev/mmcblk0p2 /\r"
expect "root@(none):/# "
send "passwd pi\r"
expect "New password:"
send "$loginPassword\r"
expect "Retype new password:"
send "$loginPassword\r"
expect "root@(none):/# "
send "sync\r"
expect "root@(none):/# "
send "exec /sbin/init\r"

# Login process
expect {
  "raspberrypi login: " {
    send "$loginUser\r"
    exp_continue
  }
  "Password: " {
    send "$loginPassword\r"
    exp_continue
  }
  "pi@raspberrypi:~$ " {
    send "sudo date -s \"DATESED\"\r"
  }
  "Login incorrect" {
    exit 1
  }
}

# Install tests
expect "pi@raspberrypi:~$ "
send "sudo /opt/zram-config/install.bash\r"
expect "pi@raspberrypi:~$ "
send "/opt/zram-config/tests/test-zram-devices.bash\r"
expect {
  -re "Test failed:.*$" {
    set retval 1
    exp_continue
  }
  "pi@raspberrypi:~$ " {
    send "sudo touch /var/log/test\r"
  }
}

# Uninstall tests
expect "pi@raspberrypi:~$ "
send "sudo /opt/zram-config/uninstall.bash\r"
expect "pi@raspberrypi:~$ "
send "/opt/zram-config/tests/test-zram-devices.bash removal\r"
expect {
  -re "Test failed:.*$" {
    set retval 1
    exp_continue
  }
  "pi@raspberrypi:~$ " {
    send "sync\r"
  }
}

# Shutdown
expect "pi@raspberrypi:~$ "
send "sudo shutdown -h now\r"

exit $retval
